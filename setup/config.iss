; Script generated by the Inno Script Studio Wizard.
#define MyAppName "TrayMonitor"
#define MyAppVersion "2.3.0"
#define MyAppPublisher "strayge"
#define MyAppURL "https://github.com/strayge/tray-monitor"

[Setup]
AppId={{D337AB36-5DC7-40A5-9127-E0273D006CD6}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
OutputDir=..\bin\Release
OutputBaseFilename=tray_monitor_{#MyAppVersion}
SolidCompression=yes
InternalCompressLevel=ultra64
PrivilegesRequired=lowest
AlwaysShowComponentsList=False
ShowComponentSizes=False
ShowLanguageDialog=no
UninstallDisplayIcon={app}\CpuIcon.exe

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "..\bin\Release\BatteryIcon.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: battery
Source: "..\bin\Release\DiskIcon.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: disk
Source: "..\bin\Release\NetIcon.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: net
Source: "..\bin\Release\RamIcon.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: ram
Source: "..\bin\Release\CpuIcon.exe"; DestDir: "{app}"; Flags: ignoreversion; Components: cpu
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Components]
Name: "battery"; Description: "Battery Icon"; Types: all
Name: "cpu"; Description: "Cpu Icon"; Types: all non_battery
Name: "ram"; Description: "Ram Icon"; Types: all non_battery
Name: "disk"; Description: "Disk Icon"; Types: all non_battery
Name: "net"; Description: "Net Icon"; Types: all non_battery

[Icons]
Name: "{group}\BatteryIcon"; Filename: "{app}\BatteryIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\BatteryIcon.exe"; IconIndex: 0; Components: battery
Name: "{group}\DiskIcon"; Filename: "{app}\DiskIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\DiskIcon.exe"; IconIndex: 0; Components: disk
Name: "{group}\NetIcon"; Filename: "{app}\NetIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\NetIcon.exe"; IconIndex: 0; Components: net
Name: "{group}\RamIcon"; Filename: "{app}\RamIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\RamIcon.exe"; IconIndex: 0; Components: ram
Name: "{group}\CpuIcon"; Filename: "{app}\CpuIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\CpuIcon.exe"; IconIndex: 0; Components: cpu
Name: "{userstartup}\BatteryIcon"; Filename: "{app}\BatteryIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\BatteryIcon.exe"; IconIndex: 0; Components: battery; Tasks: startup_battery
Name: "{userstartup}\DiskIcon"; Filename: "{app}\DiskIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\DiskIcon.exe"; IconIndex: 0; Components: disk; Tasks: startup_disk
Name: "{userstartup}\NetIcon"; Filename: "{app}\NetIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\NetIcon.exe"; IconIndex: 0; Components: net; Tasks: startup_net
Name: "{userstartup}\RamIcon"; Filename: "{app}\RamIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\RamIcon.exe"; IconIndex: 0; Components: ram; Tasks: startup_ram
Name: "{userstartup}\CpuIcon"; Filename: "{app}\CpuIcon.exe"; WorkingDir: "{app}"; IconFilename: "{app}\CpuIcon.exe"; IconIndex: 0; Components: cpu; Tasks: startup_cpu

[Tasks]
Name: "startup_battery"; Description: "Battery Icon"; GroupDescription: "Autostart on login"; Components: battery
Name: "startup_cpu"; Description: "Cpu Icon"; GroupDescription: "Autostart on login"; Components: cpu
Name: "startup_ram"; Description: "Ram Icon"; GroupDescription: "Autostart on login"; Components: ram
Name: "startup_disk"; Description: "Disk Icon"; GroupDescription: "Autostart on login"; Components: disk
Name: "startup_net"; Description: "Net Icon"; GroupDescription: "Autostart on login"; Components: net

[Types]
Name: "all"; Description: "all"
Name: "non_battery"; Description: "except Battery Icon"
Name: "custom"; Description: "custom"; Flags: iscustom

[Run]
Filename: "{app}\BatteryIcon.exe"; WorkingDir: "{app}"; Flags: nowait postinstall; Description: "run Battery Icon"; Components: battery
Filename: "{app}\CpuIcon.exe"; WorkingDir: "{app}"; Flags: nowait postinstall; Description: "run Cpu Icon"; Components: cpu
Filename: "{app}\RamIcon.exe"; WorkingDir: "{app}"; Flags: nowait postinstall; Description: "run Ram Icon"; Components: ram
Filename: "{app}\DiskIcon.exe"; WorkingDir: "{app}"; Flags: nowait postinstall; Description: "run Disk Icon"; Components: disk
Filename: "{app}\NetIcon.exe"; WorkingDir: "{app}"; Flags: nowait postinstall; Description: "run Net Icon"; Components: net

[Code]
function IsAnyComponentSelected(): Boolean;
var
  I: Integer;
begin
  Result := False;
  for I := 0 to WizardForm.ComponentsList.Items.Count - 1 do
    if WizardForm.ComponentsList.Checked[I] then
    begin
      Result := True;
      Exit;
    end;
end;

function NextButtonClick(PageID: Integer): Boolean;
begin
  Result:= True;
  if (PageID = wpSelectComponents) and not IsAnyComponentSelected() then begin
    MsgBox('No items selected, please select at least one item', mbError, MB_OK);
    Result := False;
  end;
end;
